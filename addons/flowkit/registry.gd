extends Node
class_name FKRegistry

# Preload the expression evaluator
const FKExpressionEvaluator = preload("res://addons/flowkit/runtime/expression_evaluator.gd")

# Path to the provider manifest resource
const MANIFEST_PATH = "res://addons/flowkit/saved/provider_manifest.tres"

var action_providers: Array = []
var condition_providers: Array = []
var event_providers: Array = []
var behavior_providers: Array = []
var branch_providers: Array = []

## O(1) lookup maps rebuilt after load
var _action_by_id: Dictionary = {}
var _condition_by_id: Dictionary = {}
var _event_by_id: Dictionary = {}
var _behavior_by_id: Dictionary = {}
var _branch_by_id: Dictionary = {}

func load_all() -> void:
	action_providers.clear()
	condition_providers.clear()
	event_providers.clear()
	behavior_providers.clear()
	branch_providers.clear()
	# Try to load from manifest first (required for exported builds)
	if _load_from_manifest():
		_rebuild_indexes()
		print("[FKRegistry] Loaded providers from manifest: %d actions, %d conditions, %d events, %d behaviors, %d branches" % [
			action_providers.size(),
			condition_providers.size(),
			event_providers.size(),
			behavior_providers.size(),
			branch_providers.size()
		])
		return
	
	# Fallback to directory scanning (editor/development only)
	# This will not work in exported builds where DirAccess cannot enumerate files
	if OS.has_feature("editor"):
		_load_folder("actions", action_providers)
		_load_folder("conditions", condition_providers)
		_load_folder("events", event_providers)
		_load_folder("behaviors", behavior_providers)
		_load_folder("branches", branch_providers)
		_rebuild_indexes()
		
		print("[FKRegistry]: Loaded providers from directories: %d actions, %d conditions, %d events, %d behaviors, %d branches" % [
			action_providers.size(),
			condition_providers.size(),
			event_providers.size(),
			behavior_providers.size(),
			branch_providers.size()
		])
	else:
		push_error("[FKRegistry]: No provider manifest found and directory scanning is not available in exported builds. Generate the manifest in the editor.")

func load_providers() -> void:
	# Alias for load_all() for backward compatibility
	load_all()

func _rebuild_indexes() -> void:
	_action_by_id.clear()
	_condition_by_id.clear()
	_event_by_id.clear()
	_behavior_by_id.clear()
	_branch_by_id.clear()
	for p in action_providers:
		if p and p.has_method("get_id"):
			_action_by_id[p.get_id()] = p
	for p in condition_providers:
		if p and p.has_method("get_id"):
			_condition_by_id[p.get_id()] = p
	for p in event_providers:
		if p and p.has_method("get_id"):
			_event_by_id[p.get_id()] = p
	for p in behavior_providers:
		if p and p.has_method("get_id"):
			_behavior_by_id[p.get_id()] = p
	for p in branch_providers:
		if p and p.has_method("get_id"):
			_branch_by_id[p.get_id()] = p

## Load providers from the pre-generated manifest resource.
## Returns true if successful, false if manifest not found or invalid.
func _load_from_manifest() -> bool:
	if not ResourceLoader.exists(MANIFEST_PATH):
		return false
	
	var manifest: Resource = load(MANIFEST_PATH)
	if not manifest:
		return false
	
	# Instantiate providers from the manifest scripts
	# Base classes (no get_id) may be in the manifest to satisfy inheritance
	# but should not be instantiated as providers.
	if manifest.get("action_scripts"):
		for script: GDScript in manifest.action_scripts:
			_try_instantiate_provider(script, action_providers)

	if manifest.get("condition_scripts"):
		for script: GDScript in manifest.condition_scripts:
			_try_instantiate_provider(script, condition_providers)

	if manifest.get("event_scripts"):
		for script: GDScript in manifest.event_scripts:
			_try_instantiate_provider(script, event_providers)

	if manifest.get("behavior_scripts"):
		for script: GDScript in manifest.behavior_scripts:
			_try_instantiate_provider(script, behavior_providers)

	if manifest.get("branch_scripts"):
		for script: GDScript in manifest.branch_scripts:
			_try_instantiate_provider(script, branch_providers)
	
	var has_providers = action_providers.size() + condition_providers.size() + event_providers.size() + behavior_providers.size() + branch_providers.size() > 0
	return has_providers


## Safely instantiate a provider from a script.
## Skips base classes (no get_id) and scripts that fail to load.
func _try_instantiate_provider(script: GDScript, array: Array) -> void:
	if not script:
		return
	# can_instantiate() returns false if the script has parse errors
	if not script.can_instantiate():
		push_warning("[FlowKit Registry] Skipping script that cannot be instantiated: %s" % script.resource_path)
		return
	var instance = script.new()
	# Base/utility classes included for inheritance won't have get_id — skip them
	if not instance.has_method("get_id"):
		return
	array.append(instance)


## Directory scanning for editor/development use only.
## This will NOT work in exported builds.
func _load_folder(subpath: String, array: Array) -> void:
	var path: String = "res://addons/flowkit/" + subpath
	_scan_directory_recursive(path, array)

func _scan_directory_recursive(path: String, array: Array) -> void:
	var dir: DirAccess = DirAccess.open(path)
	if not dir:
		return
	
	dir.list_dir_begin()
	var file_name: String = dir.get_next()
	
	while file_name != "":
		var file_path: String = path + "/" + file_name
		
		if dir.current_is_dir():
			# Recursively scan subdirectories
			_scan_directory_recursive(file_path, array)
		elif file_name.ends_with(".gd") and not file_name.ends_with(".uid"):
			# Load the script and instantiate it
			var script: Variant = load(file_path)
			if script:
				var instance: Variant = script.new()
				array.append(instance)
		
		file_name = dir.get_next()
	
	dir.list_dir_end()

func poll_event(event_id: String, node: Node, inputs: Dictionary = {}, block_id: String = "", scene_root: Node = null) -> bool:
	var provider = _event_by_id.get(event_id, null)
	if provider and provider.has_method("poll"):
		var evaluated_inputs: Dictionary = FKExpressionEvaluator.evaluate_inputs(inputs, node, scene_root)
		return provider.poll(node, evaluated_inputs, block_id)
	return false

## Returns the event provider instance for the given event_id, or null.
func get_event_provider(event_id: String) -> Variant:
	return _event_by_id.get(event_id, null)

## Create a new, independent instance of the event provider for the given event_id.
## Each event block should get its own instance to avoid shared state bugs.
func create_event_instance(event_id: String) -> Variant:
	var provider = _event_by_id.get(event_id, null)
	if provider:
		return provider.get_script().new()
	return null

## Call setup() on an event provider so it can connect to signals on the target node.
## trigger_callback is a Callable the provider can call to fire the block immediately.
func setup_event(event_id: String, node: Node, trigger_callback: Callable, block_id: String = "") -> void:
	var provider: Variant = get_event_provider(event_id)
	if provider and provider.has_method("setup"):
		provider.setup(node, trigger_callback, block_id)

## Call teardown() on an event provider so it can disconnect signals / clean up.
func teardown_event(event_id: String, node: Node, block_id: String = "") -> void:
	var provider: Variant = get_event_provider(event_id)
	if provider and provider.has_method("teardown"):
		provider.teardown(node, block_id)

## Returns true if the event provider with the given id is a signal-based event.
func is_signal_event(event_id: String) -> bool:
	var provider: Variant = get_event_provider(event_id)
	if provider and provider.has_method("is_signal_event"):
		return provider.is_signal_event()
	return false

func check_condition(condition_id: String, node: Node, inputs: Dictionary, negated: bool = false, scene_root: Node = null, block_id: String = "") -> bool:
	var provider = _condition_by_id.get(condition_id, null)
	if provider and provider.has_method("check"):
		var context = node
		var evaluated_inputs: Dictionary = FKExpressionEvaluator.evaluate_inputs(inputs, context, scene_root, node)
		var result = provider.check(node, evaluated_inputs, block_id)
		return not result if negated else result
	return false

func execute_action(action_id: String, node: Node, inputs: Dictionary, scene_root: Node = null, block_id: String = "") -> Variant:
	var provider = _action_by_id.get(action_id, null)
	if provider and provider.has_method("execute"):
		# Use scene_root as the base instance so get_node() resolves from the scene root
		# Pass original node as target_node so n_ variable lookups resolve on the correct node
		var context = scene_root if scene_root else node
		var evaluated_inputs: Dictionary = FKExpressionEvaluator.evaluate_inputs(inputs, context, scene_root, node)
		
		# Per-invocation wait token so concurrent multi-frame actions never share state.
		var is_multi_frame_action: bool = provider.has_method("requires_multi_frames") and provider.requires_multi_frames()
		var wait_token := {"done": false}
		var on_completed := func():
			wait_token["done"] = true
		
		if is_multi_frame_action:
			provider.exec_completed.connect(on_completed)
		
		provider.execute(node, evaluated_inputs, block_id)
		
		if is_multi_frame_action:
			while not wait_token["done"]:
				if not is_instance_valid(node) or not node.get_tree():
					break
				await node.get_tree().process_frame
			if provider.exec_completed.is_connected(on_completed):
				provider.exec_completed.disconnect(on_completed)
		
		return provider
	return null

func get_behavior(behavior_id: String) -> Variant:
	return _behavior_by_id.get(behavior_id, null)

func apply_behavior(behavior_id: String, node: Node, inputs: Dictionary = {}, scene_root: Node = null) -> void:
	var behavior: Variant = get_behavior(behavior_id)
	if behavior and behavior.has_method("apply"):
		# Use scene_root as context if provided, otherwise use the node
		var context = scene_root if scene_root else node
		var evaluated_inputs: Dictionary = FKExpressionEvaluator.evaluate_inputs(inputs, context, scene_root)
		behavior.apply(node, evaluated_inputs)

func remove_behavior(behavior_id: String, node: Node) -> void:
	var behavior: Variant = get_behavior(behavior_id)
	if behavior and behavior.has_method("remove"):
		behavior.remove(node)

# --- Branch providers -------------------------------------------------------

func get_branch_provider(branch_id: String) -> Variant:
	return _branch_by_id.get(branch_id, null)

## Resolve the branch provider ID for a branch action.
## Provides backward compatibility: legacy sheets stored "if"/"elseif"/"else"
## in branch_type without a separate branch_id field.
func resolve_branch_id(act_branch_id: String, act_branch_type: String) -> String:
	if act_branch_id and not act_branch_id.is_empty():
		return act_branch_id
	# Legacy compatibility
	if act_branch_type in ["if", "elseif", "else"]:
		return "if_branch"
	return ""

## Evaluate branch inputs through the expression evaluator.
func evaluate_branch_inputs(inputs: Dictionary, scene_root: Node) -> Dictionary:
	if inputs.is_empty():
		return {}
	var context = scene_root if scene_root else null
	return FKExpressionEvaluator.evaluate_inputs(inputs, context, scene_root)
