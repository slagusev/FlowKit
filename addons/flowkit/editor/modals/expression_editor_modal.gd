@tool
extends FKModalWindow
class_name FKExpressionEditorModal

var selected_node_path: String = ""
var selected_action_id: String = ""
var action_inputs: Array = []
var current_param_index: int = 0
var param_values: Dictionary = {}

@export var param_label: Label
@export var expression_input: LineEdit 
@export var description_label: Label
@export var description_panel: Control
@export var node_tree: Tree 
@export var item_list: ItemList 
@export var prev_button: Button
@export var next_button: Button 
@export var confirm_button: Button

var selected_tree_node: Node = null

func _enter_tree() -> void:
	super._enter_tree()
	
	if is_editor_preview:
		return
		
	_setup_node_tree.call_deferred()

func _toggle_subs(on: bool):
	if on and not _is_subbed:
		node_tree.item_selected.connect(_on_node_selected)
		item_list.item_activated.connect(_on_item_activated)
		if expression_input and not expression_input.text_changed.is_connected(_on_expression_text_changed):
			expression_input.text_changed.connect(_on_expression_text_changed)
	elif _is_subbed and not on:
		node_tree.item_selected.disconnect(_on_node_selected)
		item_list.item_activated.disconnect(_on_item_activated)
		if expression_input and expression_input.text_changed.is_connected(_on_expression_text_changed):
			expression_input.text_changed.disconnect(_on_expression_text_changed)
	else:
		return
	
	_is_subbed = on

var _validate_label: Label

func _on_expression_text_changed(new_text: String) -> void:
	_live_validate(new_text)

func _live_validate(text: String) -> void:
	if _validate_label == null and expression_input:
		_validate_label = Label.new()
		_validate_label.add_theme_font_size_override("font_size", 11)
		var parent = expression_input.get_parent()
		if parent:
			parent.add_child(_validate_label)
	if _validate_label == null:
		return
	var t := text.strip_edges()
	if t.is_empty():
		_validate_label.text = ""
		return
	# Lightweight syntax-ish checks (full eval needs runtime context)
	if t.count("(") != t.count(")"):
		_validate_label.text = "⚠ Unbalanced parentheses"
		_validate_label.add_theme_color_override("font_color", Color(1, 0.5, 0.4))
		return
	if t.count("\"") % 2 != 0 and t.count("'") % 2 != 0:
		_validate_label.text = "⚠ Unbalanced quotes"
		_validate_label.add_theme_color_override("font_color", Color(1, 0.5, 0.4))
		return
	_validate_label.text = "✓ Looks ok (runtime may still fail)"
	_validate_label.add_theme_color_override("font_color", Color(0.5, 0.9, 0.6))

func populate_inputs(node_path: String, action_id: String, inputs: Array, \
current_values: Dictionary = {}) -> void:
	selected_node_path = node_path
	selected_action_id = action_id
	action_inputs = inputs
	current_param_index = 0
	param_values = current_values.duplicate()

	_show_current_parameter()
	
	# Setup node tree if editor interface is available
	_setup_node_tree()

func _setup_node_tree() -> void:
	node_tree.clear()
	
	if not _scene_root:
		return
	
	_add_sys_node_entry()
	var root_item := _create_root_item()
	_add_node_children(_scene_root, root_item)


func _add_sys_node_entry():
	# The System Node should be a runtime Autoload.
	var system_item: TreeItem = node_tree.create_item()
	system_item.set_text(0, "System (FlowKitSystem)")
	system_item.set_metadata(0, null)  # No actual node in editor
	system_item.set_icon(0, _node_theme_icon)
	
var _node_theme_icon: Texture2D:
	get:
		return _base_control.get_theme_icon("Node", "EditorIcons")
		
func _create_root_item() -> TreeItem:
	var root_item: TreeItem = node_tree.create_item()
	root_item.set_text(0, _scene_root.name)
	root_item.set_metadata(0, _scene_root)
	root_item.set_icon(0, _node_theme_icon)
	return root_item
	

func _add_node_children(node: Node, tree_item: TreeItem) -> void:
	for child in node.get_children():
		var child_item: TreeItem = tree_item.create_child()
		child_item.set_text(0, child.name)
		child_item.set_metadata(0, child)
		
		# Get node icon from editor
		var icon_name: String = child.get_class()
		var icon: Texture2D = _base_control.get_theme_icon(icon_name, "EditorIcons")
		if icon:
			child_item.set_icon(0, icon)
		
		# Recursively add this node's children
		if child.get_child_count() > 0:
			_add_node_children(child, child_item)


func _show_current_parameter() -> void:
	var current_input = action_inputs[current_param_index]
	var param_name: String; var param_type: String; var param_description: String;
	var fk_action_input: FKActionInput
	if current_input is Dictionary:
		var param_dict: Dictionary = action_inputs[current_param_index]
		param_name = param_dict.get("name", "Unknown")
		param_type = param_dict.get("type", "Variant")
		param_description = param_dict.get("description", "")
		
	elif current_input is FKActionInput:
		fk_action_input = current_input
		param_name = fk_action_input.name
		param_type = fk_action_input.type
		param_description = fk_action_input.description
		
	
	param_label.text = "%s (%s)" % [param_name, param_type]
	# ^For some reason, this doesn't work when we assign the format to a var...
	_update_desc(param_description)
	_update_expr_input(param_name)
	_update_nav_buttons()
	
func _update_desc(param_description: String):
	description_label.text = param_description
	var is_there_desc_to_show: bool = param_description.length() > 0
	description_panel.visible = is_there_desc_to_show

func _update_expr_input(param_name: String):
	expression_input.text = param_values.get(param_name, "")
	expression_input.grab_focus()
	expression_input.caret_column = expression_input.text.length()

func _update_nav_buttons():
	prev_button.disabled = current_param_index == 0
	next_button.disabled = current_param_index >= action_inputs.size() - 1
	confirm_button.text = "Confirm" if current_param_index >= action_inputs.size() - 1 \
	else "Next"
	
func _on_node_selected() -> void:
	var selected_item: TreeItem = node_tree.get_selected()
	if not selected_item:
		return
	
	selected_tree_node = selected_item.get_metadata(0)
	_populate_item_list_for_selected_node()

func _populate_item_list_for_selected_node() -> void:
	item_list.clear()
	
	# Special handling for System node (null metadata)
	if selected_tree_node == null:
		_add_system_snippets()
		return
	
	var target_node: Node = _scene_root.get_node_or_null(selected_node_path) if _scene_root \
	else null
	
	_add_var_items(target_node)
	_add_prop_items(target_node)
	_add_math_op_section()
	_add_type_helpers()

func _add_system_snippets() -> void:
	item_list.add_item("system.get_var(\"variable_name\")")
	item_list.add_item("system.get_sheet_var(\"score\")")
	item_list.add_item("s_score")
	item_list.add_item("delta")
	item_list.add_item("current")
	item_list.add_item("current.global_position")
	item_list.add_item("true")
	item_list.add_item("false")
	item_list.add_item("null")
	item_list.add_item("Vector2(0, 0)")
	item_list.add_item("Vector3(0, 0, 0)")
	item_list.add_item("Color(1, 1, 1, 1)")
	_add_type_helpers()

func _add_type_helpers() -> void:
	item_list.add_item("--- helpers ---")
	item_list.add_item("true")
	item_list.add_item("false")
	item_list.add_item("Vector2(0, 0)")
	item_list.add_item("Vector2(1, 0)")
	item_list.add_item("Color(1, 1, 1, 1)")
	item_list.add_item("\"text\"")

var _scene_root: Node:
	get:
		return _editor_interface.get_edited_scene_root()
		
func _add_var_items(target_node: Node):
	if not selected_tree_node.has_meta("flowkit_variables"):
		return
	
	var vars: Dictionary = selected_tree_node.get_meta("flowkit_variables", {})
	
	if _is_target_node(selected_tree_node):
		_add_targ_var_items(vars)
	else:
		_add_other_node_var_items(vars, target_node)

func _add_targ_var_items(vars: Dictionary):
	for var_name in vars.keys():
		item_list.add_item("n_" + var_name)


func _add_other_node_var_items(vars: Dictionary, target_node: Node) -> void:
	for var_name in vars.keys():
		var ref := _build_cross_node_var_ref(var_name, target_node)
		item_list.add_item(ref)
		
		
func _build_cross_node_var_ref(var_name: String, target_node: Node) -> String:
	# Since we want to make sure (when possible) that the inputs for things _not_ targeting 
	# the System node are evaluated using the target node as the ref point.
	# Case 1: path from target → selected
	if target_node and _scene_root:
		var path_from_target: String = str(target_node.get_path_to(selected_tree_node))
		return 'system.get_node_var(node.get_node("' + path_from_target + '"), "' + var_name + '")'
	
	# Case 2: fallback path from root → selected
	if _scene_root:
		var path_from_root: String = str(_scene_root.get_path_to(selected_tree_node))
		if path_from_root == ".":
			# Selected node IS the scene root
			return 'system.get_node_var(node.get_tree().current_scene, "' + var_name + '")'
		else:
			return 'system.get_node_var(node.get_tree().current_scene.get_node("' + path_from_root + '"), "' + var_name + '")'
	
	# Extremely defensive fallback (shouldn’t normally hit)
	return 'system.get_node_var(node.get_tree().current_scene, "' + var_name + '")'
	
func _add_prop_items(target_node: Node):
	var properties = []
	
	# Check if this is the target node for property references
	if _is_target_node(selected_tree_node):
		# Target node - use 'node.' prefix
		properties = [
			"node.name",
			"node.position",
			"node.position.x",
			"node.position.y",
			"node.rotation",
			"node.scale",
			"node.scale.x",
			"node.scale.y",
			"node.visible",
			"node.modulate"
		]
		
		# Add type-specific properties
		if selected_tree_node is CharacterBody2D:
			properties.append_array([
				"node.velocity",
				"node.velocity.x",
				"node.velocity.y"
			])
		elif selected_tree_node is Camera2D:
			properties.append_array([
				"node.zoom",
				"node.offset"
			])
	else:
		# Different node - use get_node() reference
		if _scene_root:
			var absolute_path: String = str(_scene_root.get_path_to(selected_tree_node))
			var node_ref: String = 'get_node("' + absolute_path + '")'
			
			properties = [
				node_ref + ".name",
				node_ref + ".position",
				node_ref + ".position.x",
				node_ref + ".position.y",
				node_ref + ".rotation",
				node_ref + ".scale.x",
				node_ref + ".scale.y",
				node_ref + ".visible"
			]
			
			if selected_tree_node is CharacterBody2D:
				properties.append_array([
					node_ref + ".velocity",
					node_ref + ".velocity.x",
					node_ref + ".velocity.y"
				])
	
	for prop in properties:
		item_list.add_item(prop)

func _is_target_node(node: Node) -> bool:
	if not _scene_root:
		return false
	return node == _scene_root.get_node_or_null(selected_node_path)
	
func _add_math_op_section():
	item_list.add_item("─────────────────")
	item_list.set_item_disabled(item_list.item_count - 1, true)
	item_list.add_item("+ (Add)")
	item_list.add_item("- (Subtract)")
	item_list.add_item("* (Multiply)")
	item_list.add_item("/ (Divide)")
	item_list.add_item("% (Modulo)")
	item_list.add_item("abs(x)")
	item_list.add_item("ceil(x)")
	item_list.add_item("floor(x)")
	item_list.add_item("round(x)")
	item_list.add_item("sqrt(x)")
	item_list.add_item("min(a, b)")
	item_list.add_item("max(a, b)")
	item_list.add_item("clamp(val, min, max)")
	
func _on_item_activated(index: int) -> void:
	var item_text: String = item_list.get_item_text(index)
	_insert_at_cursor(item_text)

func _insert_at_cursor(text: String) -> void:
	# Extract just the value part (before any description in parentheses)
	var insert_text: String = text.split(" (")[0]
	
	# Get cursor position
	var cursor_pos: int = expression_input.caret_column
	var current_text: String = expression_input.text
	
	# Insert at cursor
	var before: String = current_text.substr(0, cursor_pos)
	var after: String = current_text.substr(cursor_pos)
	
	expression_input.text = before + insert_text + after
	expression_input.caret_column = cursor_pos + insert_text.length()
	expression_input.grab_focus()

func _save_current_parameter() -> void:
	if action_inputs.is_empty():
		return
	
	var param_data = action_inputs[current_param_index]
	var param_name: String
	if param_data is Dictionary:
		param_name = param_data.get("name", "")
	elif param_data is FKActionInput:
		param_name = param_data.name
		
	if expression_input:
		param_values[param_name] = expression_input.text

func _on_prev_button_pressed() -> void:
	_save_current_parameter()
	if current_param_index > 0:
		current_param_index -= 1
		_show_current_parameter()

func _on_next_button_pressed() -> void:
	_save_current_parameter()
	if current_param_index < action_inputs.size() - 1:
		current_param_index += 1
		_show_current_parameter()
	else:
		_confirm()

func _on_confirm_button_pressed() -> void:
	_save_current_parameter()
	
	if current_param_index < action_inputs.size() - 1:
		# Move to next parameter
		current_param_index += 1
		_show_current_parameter()
	else:
		# Confirm all
		_confirm()

func _confirm() -> void:
	_save_current_parameter()
	_modal_signals.expressions_confirmed.emit(selected_node_path, selected_action_id, \
	param_values)
	hide()

func _on_cancel_button_pressed() -> void:
	hide()
	
